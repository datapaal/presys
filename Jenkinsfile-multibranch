node {
    def project = "navikt"
    def application = "presys"

    def mvnHome = tool "maven-3.3.9"

    def commitHash, commitHashShort, commitUrl, committer, deploymentId

    try {
        cleanWs()

        stage("checkout") {
            checkout scm
        }

        stage("initialize") {
            pom = readMavenPom file: 'pom.xml'

            if (!pom.version.contains("-SNAPSHOT")) {
                error("Expected a SNAPSHOT version")
            }

            commitHash = sh(script: 'git rev-parse HEAD', returnStdout: true).trim()
            commitHashShort = sh(script: 'git rev-parse --short HEAD', returnStdout: true).trim()
            commitUrl = "https://github.com/${project}/${application}/commit/${commitHash}"

            /* gets the person who committed last as "Surname, First name" */
            committer = sh(script: 'git log -1 --pretty=format:"%an"', returnStdout: true).trim()
        }

        stage("build") {
            dir ("klient") {
                withEnv(['HTTP_PROXY=http://webproxy-utvikler.nav.no:8088', 'NO_PROXY=adeo.no']) {
                    sh "npm install"
                }
            }

            withEnv(["PATH+MAVEN=${mvnHome}/bin"]) {
                sh "mvn clean install -Djava.io.tmpdir=/tmp/${application} -B -e"
            }

            dir ("server") {
                sh "nais validate"
                sh "docker build --pull -t docker.adeo.no:5000/${application}:${commitHashShort} ."
            }
        }

        // in a multibranch pipeline, when using "GitHub Branch Source" plugin with "Discover pull requests",
        // the PR number is available in the CHANGE_ID environment variable.
        // because the same Jenkinsfile is used for both PR builds and branch builds,
        // we have to check for the existence of CHANGE_ID
        if (env.CHANGE_ID) {
            stage("sonar analysis") {
                def scannerHome = tool 'sonarqube-scanner';

                // withSonarQubeEnv injects SONAR_HOST_URL and SONAR_AUTH_TOKEN (amongst others),
                // so we don't have to set them as cli args to sonar-scanner
                withSonarQubeEnv('Presys Sonar') {
                    withCredentials([string(credentialsId: 'navikt-ci-oauthtoken', variable: 'GITHUB_OAUTH_TOKEN')]) {
                        withEnv(['SONAR_SCANNER_OPTS=-Dhttps.proxyHost=webproxy-utvikler.nav.no -Dhttps.proxyPort=8088 -Dhttp.nonProxyHosts=adeo.no']) {
                            sh """
                                ${scannerHome}/bin/sonar-scanner \
                                    -Dsonar.projectVersion=${pom.version} \
                                    -Dsonar.analysis.mode=preview \
                                    -Dsonar.github.pullRequest=${env.CHANGE_ID} \
                                    -Dsonar.github.oauth=${env.GITHUB_OAUTH_TOKEN}
                            """
                        }
                    }
                }
            }
        }

        stage("integration tests") {
            withCredentials([usernamePassword(credentialsId: 'presysDB_U', usernameVariable: 'SPRING_DATASOURCE_USERNAME', passwordVariable: 'SPRING_DATASOURCE_PASSWORD'),
                             usernamePassword(credentialsId: 'srvpresys', usernameVariable: 'SERVICEUSER_USERNAME', passwordVariable: 'SERVICEUSER_PASSWORD'),
                             certificate(aliasVariable: '', credentialsId: 'nav_truststore', keystoreVariable: 'NAV_TRUSTSTORE_PATH', passwordVariable: 'NAV_TRUSTSTORE_PASSWORD')]) {
                sh """
                    docker run --name ${application}-${commitHashShort} --rm -dP \
                        -e NAV_TRUSTSTORE_PATH=/app/cacerts \
                        -e NAV_TRUSTSTORE_PASSWORD \
                        -e SPRING_DATASOURCE_URL='jdbc:oracle:thin:@(DESCRIPTION=(ADDRESS_LIST=(ADDRESS=(PROTOCOL=TCP)(HOST=d26dbfl023.test.local)(PORT=1521)))(CONNECT_DATA=(SERVICE_NAME=PRESYSCDU1)(INSTANCE_NAME=ccuf02)(UR=A)(SERVER=DEDICATED)))' \
                        -e SPRING_DATASOURCE_USERNAME \
                        -e SPRING_DATASOURCE_PASSWORD \
                        -e ABAC_URL=https://wasapp-t0.adeo.no/asm-pdp/authorize \
                        -e SERVICEUSER_USERNAME \
                        -e SERVICEUSER_PASSWORD \
                        -e JWT_PASSWORD=somesecret \
                        -e LDAP_URL=ldaps://ldapgw.preprod.local \
                        -e LDAP_BASEDN=dc=preprod,dc=local \
                        -v ${NAV_TRUSTSTORE_PATH}:/app/cacerts \
                        docker.adeo.no:5000/${application}:${commitHashShort}
                """
            }

            dir ("qa") {
                withEnv(['HTTP_PROXY=http://webproxy-utvikler.nav.no:8088', 'NO_PROXY=adeo.no']) {
                    sh "npm install"
                }

                dockerPort = sh(script: "docker port ${application}-${commitHashShort} 8080/tcp | sed s/.*://", returnStdout: true).trim()

                // wait for app to become ready
                timeout(time: 180, unit: 'SECONDS') {
                    sh "until curl -o /dev/null -s --head --fail http://localhost:${dockerPort}/isReady; do sleep 1; done"
                }

                sh "PORT=${dockerPort} ./node_modules/.bin/nightwatch --env jenkins"
            }

            sh "docker stop ${application}-${commitHashShort} || true"
        }

        stage("release snapshot") {
            sh "docker push docker.adeo.no:5000/${application}:${commitHashShort}"

            dir ("server") {
                withCredentials([[$class: 'UsernamePasswordMultiBinding', credentialsId: 'nexusUser', usernameVariable: 'NEXUS_USERNAME', passwordVariable: 'NEXUS_PASSWORD']]) {
                    sh "nais upload --app ${application} -v ${commitHashShort}"
                }
            }
        }

        stage("deploy") {
            build([
                job: 'presys-deploy-pipeline',
                propagate: false,
                parameters: [
                    string(name: 'RELEASE_VERSION', value: commitHashShort),
                    string(name: 'COMMIT_HASH', value: commitHash),
                    string(name: 'DEPLOY_ENV', value: 'q0')
                ]
            ])
        }

        slackSend([
            color: 'good',
            message: "Build <${env.BUILD_URL}|#${env.BUILD_NUMBER}> (<${commitUrl}|${commitHashShort}>) of ${project}/${application}@${env.BRANCH_NAME} by ${committer} passed"
        ])

        currentBuild.result = 'SUCCESS'
    } catch (e) {
        sh "docker stop ${application}-${commitHashShort} || true"

        slackSend([
            color: 'danger',
            message: "Build <${env.BUILD_URL}|#${env.BUILD_NUMBER}> (<${commitUrl}|${commitHashShort}>) of ${project}/${application}@${env.BRANCH_NAME} by ${committer} failed"
        ])

        currentBuild.result = 'FAILED'
        throw e
    }
}
